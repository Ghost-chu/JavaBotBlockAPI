plugins{
    id 'com.jfrog.bintray' version '1.8.4'
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'maven'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

def ver = new Version(major: 3, minor: 3, revision: 2)

group = "com.andre601"
version = "$ver"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

jar{
    setArchivesBaseName('JavaBotBlockAPI')
}

repositories{
    mavenCentral()
    jcenter()
}

dependencies{
    api group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.3.0'
    api group: 'org.json', name: 'json', version: '20190722'
    api group: 'org.jetbrains', name: 'annotations', version: '18.0.0'
    api(group: 'net.dv8tion', name: 'JDA', version: '4.1.0_90'){
        exclude(module: 'opus-java')
    }
    api group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '2.8.0'
}

task sourcesJar(type: Jar, dependsOn: classes){
    getArchiveClassifier().set("sources")
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc){
    getArchiveClassifier().set("javadoc")
    from(javadoc.destinationDir)
}

artifacts{
    archives sourcesJar
    archives javadocJar
}

bintray{
    user = System.getenv("bintrayUser")
    key = System.getenv("bintrayApiKey")
    publications = ['JavaBotBlockAPIRelease']
    pkg{
        repo = 'maven'
        name = 'JavaBotBlockAPI'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/botblock/JavaBotBlockAPI'
        version{
            name = project.version
            released = new Date()
        }
    }
}

publishing{
    publications{
        JavaBotBlockAPIRelease(MavenPublication){
            from components.java
            groupId group
            artifactId archivesBaseName
            version version
            artifact(javadocJar)
            artifact(sourcesJar)
        }
    }
}

class Version{
    String major, minor, revision
    
    static String getBuild(){
        System.getenv("BUILD_NUMBER") ?: System.getProperty("BUILD_NUMBER") ?: 
        System.getenv("GIT_COMMIT")?.substring(0, 7) ?: System.getProperty("GIT_COMMIT")?.substring(0, 7) ?: "dev"
    }
    
    String toString(){
        "$major.$minor.${revision}_$build"
    }
}
